// DroidMate, an automated execution generator for Android apps.
// Copyright (C) 2012-2016 Konrad Jamrozik
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
//
// email: jamrozik@st.cs.uni-saarland.de
// web: www.droidmate.org

/** 
 * This project exists because uiautomator2-daemon project cannot be wired normally to the build due to: https://youtrack.jetbrains.com/issue/IDEA-122904
 * Instead, it is called as external Gradle script in 'core' project build.gradle, by virtue of being a dependency
 * of task 'assembleUiautomator2Daemon'.
 * 
 * Because of uiautomator2-daemon-proxy, "Refresh all Gradle projects" will make uiautomator2-daemon visible and referable
 * in IntelliJ.
 */

import org.droidmate.buildsrc.BuildKt


apply plugin: 'java'

/* The tasks below are pruned and/or disabled as they come from Gradle's java plugin which is applied
  only to make IntelliJ happy and is not fully configured.

  There are more tasks like that that should be disabled, but I hope you know what you are doing.

  Use only "build" and "clean" tasks. */
tasks.build.dependsOn.remove("check")
tasks.build.dependsOn.remove("assemble")
tasks.check.enabled = false
tasks.assemble.enabled = false
tasks.jar.enabled = false

File uiaDaemonProjectDir = new File(rootDir, BuildKt.uia2_daemon_relative_project_dir.path)
assert uiaDaemonProjectDir.isDirectory()
File uiaDaemonSrcDir = new File(uiaDaemonProjectDir, "src/androidTest/java")
assert uiaDaemonSrcDir.isDirectory()

sourceSets {
  main.java.srcDirs = []
  // This is the magic that brings the proxied project sources into IJ.
  main.java.srcDir file(uiaDaemonSrcDir)
  test.java.srcDirs = []
  main.resources.srcDirs = []
  test.resources.srcDirs = []
  main.output.classesDir = buildDir
  test.output.classesDir = buildDir
}

repositories {
  mavenCentral()
  /*
    Why the line below is commented out and replaced with flatDir instead?
    
    It points to android extras repo. Instead of using it, I copied the necessary 
    .jar files into libs dir. This is because the extras repo contains .aar files, 
    not .jar files. The .aar files contain classes.jar files, which have to be unpacked
    and renamed.
    
    This is a workaround, i.e. I did the unpacking, renaming and copying to libs dir by hand.
     
    Correct way would be to do the unpacking. This is normally done by the android gradle plugin, 
    in tasks :uiautomator2-daemon:prepareComAndroidSupportTest*, which do the exploding of aars, 
    and link to the exploded contents (uiautomator2-daemon\build\intermediates\exploded-aar).
     
    Not sure which code exactly does all of that, but I did some Android code exploring and found 
    a good candidate for this:
    https://android.googlesource.com/platform/tools/build/+/android-6.0.1_r43/gradle/src/main/groovy/com/android/build/gradle/internal/tasks/PrepareLibraryTask.groovy#31
   */  
//  maven { url "file://" + BuildKt.android_extras_m2repo }
  flatDir { dirs "libs_copied_by_hand_from_android_extras" }
}

dependencies {
  compile project(':projects:uiautomator-daemon-lib')

  // compile files(BuildKt.uiautomator_jar.toString())
  compile files(BuildKt.android_jar_api23.toString())
  
  compile 'commons-io:commons-io:2.4'
  compile 'com.google.guava:guava:18.0'

  compile 'org.hamcrest:hamcrest-integration:1.3'
  
  compile 'junit:junit:4.12'

  /*
    The jar files for this dependency have been copied to the "flatDir" repo from
    android-sdk/extras/android/m2repository/com/android/support/support-annotations
  */
  compile 'com.android.support:support-annotations:23.3.0'

  /*
    The jar files for these dependencies have been copied to the "flatDir" repo from
    android-sdk/extras/android/m2repository/com/android/support/test
    
    They originally were in classes.jar within .aar files. See the "flatDir" repo comment for more.
  */  
  compile 'com.android.support.test:runner:0.5'
  compile 'com.android.support.test:rules:0.5'
  compile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
  
}

// Statement added to avoid Gradle bug when running "repo/dev/droidmate/gradlew test". 
// For details see the definition in repo/dev/droidmate/functions.gradle
configureTest(project, [''])